
group = 'edu.sc.seis'
version = '3.0.0beta6'

dependencies {
    compile project(':bag') { transitive = true }
    compile project(':seiswww') { transitive = true }
    compile project(':cormorant') { transitive = true }
    
    compile "jline:jline:0.9.91"
    compile "rngconvUSC:rngconv:20030225"
    compile "msv:msv:20030807"
    compile "msv:isorelax:20030807"
    compile "msv:relaxngDatatype:20030807"
    compile "msv:xsdlib:20030807"
    compile "thaiopensource:jing:20030619"
    compile "velocity-tools:velocity-tools-generic:1.4"
    compile "edu.sc.seis.mapData:dcwpo_browse:1.0"
    compile "net.sourceforge.javacsv:javacsv:2.0"
}

task tarDist(type: Tar, dependsOn: ['makeScript', 'createBuildScript', 'wrapper', 'copySodSite', 'buildSchema']) {
    compression = Compression.GZIP
    longfile = 'gnu'
    dirName = project.name+'-'+version
    from('build') {
        include('build.gradle')
        into dirName
    }
    from('build/output') {
        fileMode=0755
        include('bin/**')
        into dirName
    }
    from('build/output') {
        include('lib/**')
        into dirName
    }
    from('.') {
        include('gpl-3.0.txt')
        include('defaultProps')
        include('externalExample/**')
        include('jythonExample/**')
        include('src/**')
        include('lib/**')
        include('etc/**')
        include('gradleWrapper/**')
        include('gradlew')
        include('gradlew.bat')
        exclude('**/*.svn')
        into dirName
    }
    from('scripts') {
        include('cwg.prop')
        include('tutorial/**')
        into dirName
    }
    from('build') {
        include('doc/**')
        into dirName
    }
}

def getDExtras() {
    return [ 'seis.name':'sod-3.0.0alpha.sod', 
                'org.omg.CORBA.ORBSingletonClass':'org.jacorb.orb.ORBSingleton', 
                'python.path':'${LIB}', 
                'java.awt.headless':'true', 
                'org.omg.CORBA.ORBClass':'org.jacorb.orb.ORB', 
                'swing.volatileImageBufferEnabled':'false', 
                'java.endorsed.dirs':'${LIB}/endorsed', 
                'jacorb.connection.client.pending_reply_timeout':'120000',
                'jacorb.home':'.' ,
                'jacorb.log.default.verbosity':'2' ]
}

task makeScript(dependsOn: copyToLib) << { task ->
    dExtras = getDExtras()
//    ScriptBuilder.create('sod', 'edu.sc.seis.sod.Start', task.project, dExtras:dExtras, yourkit:true)
    ScriptBuilder.create('sod', 'edu.sc.seis.sod.Start', task.project, dExtras:dExtras)
    ScriptBuilder.create('find_events', 'edu.sc.seis.sod.tools.find_events', task.project, dExtras:dExtras)
    ScriptBuilder.create('find_stations', 'edu.sc.seis.sod.tools.find_stations', task.project, dExtras:dExtras)
    ScriptBuilder.create('find_channels', 'edu.sc.seis.sod.tools.find_channels', task.project, dExtras:dExtras)
    ScriptBuilder.create('find_seismograms', 'edu.sc.seis.sod.tools.find_seismograms', task.project, dExtras:dExtras)
    ScriptBuilder.create('find_responses', 'edu.sc.seis.sod.tools.find_responses', task.project, dExtras:dExtras)
}

task printRT << { task ->
    configurations.default.each { File f -> println f.path }
}
       


task copySodSite(dependsOn: ['copySchemaDocs', ':seiswww:makeSodSite'], type: Copy) {
    from project.file('../seiswww/build/sod/')
    into project.file('build/doc/')
}
task copySchemaDocs(dependsOn: 'buildSchemaDocs', type: Copy) {
    from project.file('build/velocity/sod/ingredients')
    into project.file('../seiswww/velocity/sod/ingredients/')
}

task buildSchemaDocs(dependsOn: ['buildSchema', classes])  { 
    inputs.dir 'src/main/relax'
    outDir = project.file('build/velocity/sod/ingredients/')
    outputs.dir outDir
    doLast{
    outDir.mkdirs()
    schemaDocArgs = project.projectDir.path+'/ '+outDir+'/'
    print schemaDocArgs
    cp = configurations.runtime.asPath+":"+project.projectDir.path+"/build/classes/main"
    ant.java(dir:outDir, 
             classname:'edu.sc.seis.sod.validator.documenter.SchemaDocumenter', 
             args:schemaDocArgs,
             fork:true,
             maxmemory:'512m',
             output:'ant_java.out',
             classpath:cp) 
    }
}

task buildSchema(dependsOn: classes)  {
    inputs.dir 'src/main/relax/'
    outputs.files 'src/main/resources/edu/sc/seis/sod/data/sod.rng'
    doLast{
    outDir = project.file('build/output')
    outDir.mkdirs()
    relaxArgs = project.projectDir.path+'/src/main/relax/sod.rng'
    ant.java(dir:outDir, 
             classname:'com.sun.msv.writer.relaxng.Driver', 
             args:relaxArgs,
             maxmemory:'512m',
             fork:true,
             classpath:configurations.runtime.asPath,
             output:project.projectDir.path+'/src/main/resources/edu/sc/seis/sod/data/sod.rng') 
    }
}

assemble.dependsOn(tarDist)
